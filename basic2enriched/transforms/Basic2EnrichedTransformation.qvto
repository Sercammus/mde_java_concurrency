modeltype BasicModel uses BasicModel;
modeltype EnrichedModel uses EnrichedModel;

transformation Basic2EnrichedTransformation(in basicModel : BasicModel, out enrichedModel: EnrichedModel);

property defaultInstanceType: EnrichedModel::InstanceType;

main() {
	basicModel.rootObjects()[BasicModel::BasicModel]->asOrderedSet()->first()-> map model2model();
}

mapping BasicModel::BasicModel::model2model() : EnrichedModel::EnrichedModel {
	
	//Create a list of instance types (= classes)
	//Start by create the class with the program entry point
	//'Main' and 'main' are invalid reference/activity names in the basic model to allow for this class
	//The class also contains any shared resources (excepting threads)
	init {
		defaultInstanceType := object EnrichedModel::InstanceType {
			name := 'Main';
		};
	}
	
	instanceTypes+=defaultInstanceType;
	instanceTypes+=self.instanceTypes->map instanceType2instanceType();
	
	methodParameters+=self.instances->map instance2instance();
	//methodParameters+=self.resources->map sharedResource2sharedResource(); 
	
	var entryPoint := object EnrichedModel::Method {
		name := "main";
		
		//Start looking through the original model
		//All activities are required in the basic model to be reachable 
		states := self.activities->map activity2state();
	};
	
	defaultInstanceType.methods += entryPoint;
	
	//Make it possible to interrupt the main thread
	self.map interruptableThread2thread();
	
	//Make a list of all method parameters that were used
	methodParameters += BasicModel::InterruptableThread.allInstances().resolveIn(BasicModel::Thread::interruptableThread2thread, EnrichedModel::Thread);
	
	end {
		
	}
}

//Instance
mapping BasicModel::Instance::instance2instance() : EnrichedModel::Instance {
	name := self.name;
	instanceType := self.instanceType.resolveoneIn(BasicModel::InstanceType::instanceType2instanceType, EnrichedModel::InstanceType);
}

//Instance type
mapping BasicModel::InstanceType::instanceType2instanceType() : EnrichedModel::InstanceType {
	name := self.name;
}

//Activity
mapping BasicModel::Activity::activity2state() : EnrichedModel::State disjuncts
	BasicModel::SimpleActivity::simpleActivity2state, BasicModel::NestedActivity::nestedActivity2state,
	BasicModel::Sleep::sleepActivity2state, BasicModel::Yield::yieldActivity2state,
	BasicModel::Interrupt::interruptActivity2state, BasicModel::Decision::decisionActivity2state,
	BasicModel::Fork::forkActivity2state, BasicModel::Join::joinActivity2state
{}

//Linear activitiy
abstract mapping BasicModel::LinearActivity::linearActivity2state() : EnrichedModel::State {
	id := self.stateId;
	
	if (self.canJumpTo(self.nextActivity)) then {
		jumps := Sequence{self.nextActivity}->map nextActivity2jump();
	} else {
		jumps := Sequence{};
	} endif;
}

query BasicModel::Activity::canJumpTo(nextActivity : BasicModel::Activity) : Boolean {
	//Do not jump to an end activity (since it does not do anything anyway)
	if (nextActivity.oclIsKindOf(BasicModel::End)) then {
		return false;
	} endif;
	
	//Only go to a join if we are currently in the thread that is supposed to join
	if (nextActivity.oclIsKindOf(BasicModel::Join)) then {
		return nextActivity.oclAsType(BasicModel::Join).joiningThread = self.thread;
	} endif;
	
	//Jump to the next activity in all other cases
	return true;
}

mapping BasicModel::Activity::nextActivity2jump() : EnrichedModel::Jump {
	nextState := self.map activity2state();
}

//Simple activity
mapping BasicModel::SimpleActivity::simpleActivity2state() : EnrichedModel::MethodCallState
	inherits BasicModel::LinearActivity::linearActivity2state
	when { self.oclIsKindOf(BasicModel::SimpleActivity) }
{
	targetInstance := self.instance.map instance2instance();
	
	//Create method that is executed by the thread
	if (self.methodCreator = self) then {
		calledMethod := object EnrichedModel::Method {
			name := self.name;
		};
		
		if (self.instanceType = null) then {
			defaultInstanceType.methods += calledMethod;
		} else {
			self.instanceType.resolveoneIn(BasicModel::InstanceType::instanceType2instanceType, EnrichedModel::InstanceType).methods += calledMethod;
		} endif;
	} else {
		calledMethod := self.methodCreator.resolveoneIn(BasicModel::SimpleActivity::simpleActivity2state, EnrichedModel::MethodCallState).calledMethod;
	} endif;
}

//Nested activity
mapping BasicModel::NestedActivity::nestedActivity2state() : EnrichedModel::MethodCallState
	inherits BasicModel::LinearActivity::linearActivity2state
	when { self.oclIsKindOf(BasicModel::NestedActivity) }
{
	targetInstance := self.instance.map instance2instance();
	
	//Create method that is executed by the thread
	if (self.methodCreator = self) then {
		calledMethod := object EnrichedModel::Method {
			name := self.name;
			states := self.nestedThread.activities->map activity2state();
		};
		
		if (self.nestedThread.startActivity.instanceType = null) then {
			defaultInstanceType.methods += calledMethod;
		} else {
			self.nestedThread.startActivity.instanceType.resolveoneIn(BasicModel::InstanceType::instanceType2instanceType, EnrichedModel::InstanceType).methods += calledMethod;
		} endif;
	} else {
		calledMethod := self.methodCreator.resolveoneIn(BasicModel::NestedActivity::nestedActivity2state, EnrichedModel::MethodCallState).calledMethod;
	} endif;
}

//Sleep
mapping BasicModel::Sleep::sleepActivity2state() : EnrichedModel::SleepState
	inherits BasicModel::LinearActivity::linearActivity2state
	when { self.oclIsKindOf(BasicModel::Sleep) }
{
	duration := self.duration;
}

//Yield
mapping BasicModel::Yield::yieldActivity2state() : EnrichedModel::YieldState
	inherits BasicModel::LinearActivity::linearActivity2state
	when { self.oclIsKindOf(BasicModel::Yield) }
{
	//Do nothing.
}

//Interrupt
mapping BasicModel::Interrupt::interruptActivity2state() : EnrichedModel::InterruptState
	inherits BasicModel::LinearActivity::linearActivity2state
	when { self.oclIsKindOf(BasicModel::Interrupt) }
{
	//interruptedThread := self.interruptedThread.resolveoneIn(BasicModel::Thread::interruptableThread2thread, EnrichedModel::Thread);
	interruptedThread := self.interruptedThread.map interruptableThread2thread();
}

//Decision
mapping BasicModel::Decision::decisionActivity2state() : EnrichedModel::State
	when { self.oclIsKindOf(BasicModel::Decision) }
{
	id := self.stateId;
	jumps += self.branches->map branch2jump(self);
	
	if (self.canJumpTo(self.nextActivity)) then {
		jumps += Sequence{self.nextActivity}->map nextActivity2jump();
	} endif;
}

mapping BasicModel::Branch::branch2jump(decision : BasicModel::Decision) : EnrichedModel::Jump {
	condition := self.condition;
	
	if (decision.canJumpTo(self.nextActivity)) {
		nextState := self.nextActivity.map activity2state();
	}
}

//Fork
mapping BasicModel::Fork::forkActivity2state() : EnrichedModel::ForkState
	when { self.oclIsKindOf(BasicModel::Fork) }
{
	threadCount := self.maxThreadCount;
	threadInits := self.forkedThreadsUsingThreadInit->map forkedThread2threadInit();
	jumps := self.forkedThreadsUsingJump->map forkedThread2jump();
	id := self.stateId;
}

mapping BasicModel::ForkedThread::forkedThread2threadInit() : EnrichedModel::ThreadInit {
	isDaemon := self.daemon;
	targetInstance := self.startActivity.instance.map instance2instance();
	
	//Create method that is executed by the thread
	calledMethod := object EnrichedModel::Method {
		name := self.name;
		states := self.activities->map activity2state();
	};
	
	//Place the thread method in the instance type with the starting activity
	if (self.startActivity.instanceType = null) then {
		defaultInstanceType.methods += calledMethod;
	} else {
		self.startActivity.instanceType.resolveoneIn(BasicModel::InstanceType::instanceType2instanceType, EnrichedModel::InstanceType).methods += calledMethod;
	} endif;
	
	//Create reference to the thread (so that is can be interrupted and joined with)
	self.map interruptableThread2thread();
}

mapping BasicModel::ForkedThread::forkedThread2jump() : EnrichedModel::Jump {
	nextState := self.startActivity.map activity2state();
}

mapping BasicModel::Thread::interruptableThread2thread() : EnrichedModel::Thread {
	name := self.name;
}

//Join
mapping BasicModel::Join::joinActivity2state() : EnrichedModel::JoinState
	when { self.oclIsKindOf(BasicModel::Join) }
{
	joinedThreads += self.GetIncomingThreads()->select(e | e <> self.joiningThread)->map interruptableThread2thread();
}


